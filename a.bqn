#! ./run_once

lb ← ⌊⌾(÷⟜300)0.66×fr
Out ↩ (0.7×max) × 3 Softclip ÷⟜2
base ← Note "g1"

map ← ".---*-^-.---*.-."
Seq ← (lb÷4){(⌊𝕗×(≠-¯1⊑/)𝕨) ⌽ ∾ (𝕗×(1<+´)◶≠‿(≠|·-⟜(¯1⊸⌽)/)𝕨) ↑¨<𝕩 }⎉1

ST ← {k‿e𝕊n: (Sine⊸× k × ⋆(-e)×↕lb) × Triangle lb⥊n}
PinkP ← (2.93⋆˜↕){÷⟜(⌈´|) +` 𝕩 ↑ ⥊∘⍉∘≍´⌽ -⟜«∘(1-˜2×1|+`∘𝔽)¨ (1⌈↕∘⌈)⌾(2⋆⁼⊢)2⌈𝕩}∘≠

k ← (map='.') Seq k0 ← 160‿7e¯3 ST base
s ← (map='*') Seq 1.6 × Clip 60 Hp2 (0.25-6×·|PinkP×-⟜»)⊸× (⌽¬×˜↕⊸÷500) Fadefront 110‿2e¯3 ST 2×base
c ← (map='^') Seq 0.58 Pan (lb÷20) ⥊⟜0⊸∾ 3 Softclip 300 Hp1 (0.8 × (1⌊300÷˜↕lb) + 0.6×Sine lb⥊3×lb÷fr) × 50‿9e¯4 ST 3×base
j ← (".--.*-^...--*.--"='.') Seq k0

SB ← { 0.6 × ¯6‿270‿1.2 Peak 1(-÷+)⋆ ∾ ¯1⊸⌽⊸(((⌽↕⊸÷200)×⊢´)⊸Add¨) +˝ 6‿2 ×⍉ 6‿2.5‿5‿5 ⋈⟜4e¯5⊸ST⌜ 𝕩÷1‿2.02 }
bxm ← 500⥊⟜0⊸»¬2|(1.4‿2‿3.25×lb)⍋↕4×lb
bx ← bxm × b ← SB base
b4 ← bxm × SB ¯4 Trans base

SM ← {0.3 × 3400 Lp2 (××0⌈0.2-˜|) ∨⟜(0.24⊸×)´ Saw¨ 1‿0.49‿0.23 ×< 40 Lp2 (⌊lb÷6.2) / 𝕩 Trans 2×base}
m ← 0.52 Pan (-⌊lb×2+2÷3) ⌽ ∾ ¯1⊸↑⊸«⊸∾ (4×lb) ↑⟜SM¨ "31043843"‿"08431876"‿"06381654821"‿""-'0'

SX ← { 0.47 Pan ¯200 ⌽ (8×lb) ↑ ∾ (⌊lb×3‿5‿3÷11) ↑¨○(21⊸⥊) ((15 × 6‿2e¯2 ST 3800) Reverb 30‿6e¯3 ST Trans⟜𝕩)¨ "4310"-'0' }
x ← SX 4×base
x4 ← SX 4 Trans 4×base

yr ← ∾ (⌊lb×5‿9÷19) ↑¨○(21⊸⥊) (50‿1.2e¯3 ST Trans⟜(8×base))¨ "4938170"-'0'
y ← 0.4 Pan (8×lb) ↑ 0.6 × (××0⌈0.1-˜|) (0.1-|∘-⟜»)⊸× 400 Hp2 (Clip+¯400⌽Clip⌾(6⊸×)) yr

h ← 7500 Hp1 (+˝÷√∘≠) (⋆¯1e¯5) (⋆×1|⊢) ((7√8+π⋆˜↕120)×⌜↕lb)
hx← h ∾˜ lb↑ ∾((1-0.8×↕⊸÷)×↑⟜h)¨⌊-˜`lb÷5‿3

pi ← 0.49 Pan 0.2 × 1100 Lp1 Clip 5×Triangle (1-0.3×·Saw÷⟜4)⊸× (lb÷4) / (2×base) Trans˜ ∾⥊≍˘⟜⌽˝∧¨12|0‿6+⌜⋈⟜(4⊸-)+`↕4
p ← ¯6‿300 Lshelf pi
SQ ← {(-2×lb)↑ (⌽↕⊸÷𝕨) × +´ 𝕨 Sine∘⥊¨ ((∾"0a"+⟜↕¨10‿26)⊐𝕩) Trans 2×base}
q ← 0.2 × (-lb×3÷4)⌽∾(lb×6‿9‿6‿7÷4) SQ¨ "037a"‿"146a"‿"0479"‿"1369"

SA ← {(0.1 × 4‿2e¯3 ST 2800) (⊢´∘≢∘⊢↑˘Reverb) 4⊸×⊸+{𝔽˝∾⎉1𝔽˜˝} 0.65‿0.43 Pan˘ (Sine (lb⊸«⊸-·⌈lb×↕⊸÷)∘≠⊸/)˘ 𝕩 Trans base}
a ← SA 0‿6+⌜+`↕11
aa← SA 0‿4+⌜12+×˜↕8

(32×lb) Write ⟨k,s,c,bx,q,p,x,y⟩

be ← ((lb÷2)↑⌽↕⊸÷20) Fadeback (16×lb)⥊b
WriteFull ∾≍ Stereo¨ (((8×lb)×⊑)(+⎉1´⥊⎉1¨)1⊸↓)¨ ⟨
  ⟨1,  b ⟩
  ⟨2,  b ,x⟩
  ⟨2,  b ,x,⥊("10010110"-'0')×⌜(2×lb)↑s,(¯16×lb)↑˘c⟩
  ⟨9.5,bx,   k,s,c,m⟩
  ⟨2,  be,x⟩
  ⟨4,  bx,x ,j,s,c⟩
  ⟨1,  b4,x4,j,s  ⟩
  ⟨1,  bx,x ,j,s,c⟩
  ⟨1,  b4,x4,j,s  ⟩
  ⟨1,  bx,x ,j,s,c⟩
  ⟨2,  b ,   j,s⟩
  ⟨2,  b ,   j,s,  ⥊(2÷˜1+↕⊸÷16)×⌜h⟩
  ⟨2,  b ,   j,s,  hx,q⟩
  ⟨4,  bx,     s,  h ,q,    y,("..-.----"='.') Seq k0⟩
  ⟨2,  bx,   j,s,c,     (8×lb)_crossfade´(12×lb)⥊¨pi‿p, y⟩
  ⟨2,  bx,   k,s,  h ,  p,  y⟩
  ⟨4,  b∾b4, k,s,  hx,  p,  y⟩
  ⟨4,  bx,   k,s,  h ,q,p,x,y⟩
  ⟨4,  bx,   k,s,c,m,⌽∘↕⊸÷∘≠⊸×+´(32×lb)⥊¨h‿y⟩
  ⟨2,  be,(¯16×lb)↑˘(¯2×lb)↓˘c⟩
⟩
