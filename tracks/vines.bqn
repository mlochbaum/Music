#! ./run_once

lm ← 8 × lb ← ⌊⌾(÷⟜60)0.46×fr
Out ↩ max × 4 Softclip ÷⟜2

Ga← {{1+𝕩0⊸≠◶0‿𝕊𝕩|𝕨}¨⟜(≠+3⊸⌽)×˜⌊𝕩÷˜↕lb}
a ← 16÷˜ 4‿170‿0.8 Peak ¯8‿1100‿1.4 Peak ¯9‿2600‿0.4 Hshelf 20 Hp2 ∾8‿4‿2‿2/Ga¨17‿19‿28‿22
ax← a × ¬2|⌊⥊(0.3+"0069000201700240"-'0')×⌜×˜↕⊸÷lb

Gk← {0.5×1(-÷+)⋆5 Hp2 (2⊏∧)˘7↕(6⥊0)∾37‿1.2 (0.8⊸×+·⍋∘»⊸⊏Hp2q)⍟𝕩 lb↑3000}
⟨k7,k,k9⟩ ← Gk¨ 7‿8‿9
l ← (3⊸Rep ∾ (×˜↕⊸÷lb×7÷8)⊸Fadefront) Gk 10

c0← (0.5+4⋆˜|)⊸×1(-÷+)⋆+´7e3(+˝Sine∘(⥊×1+0.07÷4⋆I∘⊣)×⎉1 2⋆(-8-0‿3‿1‿6)×⌜I∘⊣)¨133‿360‿663
c ← 0.48 PanAP 400 ⌽ (⌊lb×1.8‿2.6‿6.24‿6.84‿7.1‿7.77‿9.8‿10.6‿15) {∾⟨0⥊˜⊑𝕨⟩∾((16×lb)⊸«⊸-𝕨)↑¨𝕩} (0.3×8‿6‿8‿3‿7‿6‿7‿6‿8÷8)×<c0

Gy← {0.6 × 0.55 PanAP {(≠⊑𝕩) ⌽ 0¨⊸∾(4×lb)↑ 10 Hp2 -⟜¬÷⟜(⌈´) ∾⌽¨𝕩} (⊒+˜⊐∾˜)⍟𝕩 ↕1}
y ← Gy 12‿14‿13‿12
z ← Gy 12‿15‿13

s ← 0.64 PanAP ⥊ ('0'-˜"011011"⊸∾˘∘‿2⥊"00100110") ×⌜ 3e3 Hp2 ÷⟜(⌈´) (lb÷4)↑(300⥊0)∾∾⟜⌽´ (⊒+˜⊐∾˜)⍟9‿11 ↕1
ss← 0.6×s
j ← 0.46 PanAP (⌊¯1.52×lb) ⌽ (4×lb) ↑ (0.3+0.6×4⋆˜|)⊸× ((2⋆¯3e¯4×↕∘≠) × ·Sine 538×¬)⍟2 Saw (lb÷2)⥊415

Gb← {(2⋆¯2.6e¯4×↕𝕨) × (Saw - 3×·Triangle÷⟜(2+3⋆˜3e¯5×↕𝕨)) 𝕨⥊𝕩}
Fb← 0.4 × 360‿1.1 Lp2q 2 Softclip 4× 50 Hp2 ⊢
Bg← {Fb ∾ Gb¨´ ⟨(lb÷4)⊸×, Trans⟜(Note"d1")⟩ {𝕎𝕩}¨ ('0'-˜' '⊸≠⊸/)¨ 𝕩}
b ← Bg ⟨
  "21222222 42122123 22222212 312212213"
  "85123407 61231768 0234/840 723076851"
⟩
be ← ((32×lb)⥊b)⊸(-∘≠⊸↓˜∾⊢)¨ (1+0.2×I∘≠)⊸×¨ <∘Bg˘ ⍉>⟨
  "22222223"‿"23111122123"‿"222222223"‿"22222223"
  "12345678"‿"10325214768"‿"103254768"‿"25364758"
⟩

SSaw ← +˝ (2.02⋆2∾-/3‿4‿1) Saw∘(⊢×1+2×3 Hp2 Pink÷√)∘×⎉0‿1 ⊢
p ← (0.5 × 2 Softclip SSaw∘/ × (÷4+(4÷lb)⊸×)∘⊒∘/∘⊣)´ ⟨⌊(lb÷2)⊸×,Trans⟜(0.7 Trans Note"d3")⟩{𝕎𝕩}¨⟨
  3‿1‿2 ‿1‿2‿2‿2‿0.7‿4 ‿ 1.3‿1‿2‿1‿2‿1.9‿3.8‿1.3
  0‿7‿10‿8‿3‿2‿3‿5  ‿5 ‿ 0  ‿2‿7‿2‿3‿8‿7  ‿10
⟩

FA ← (⊣ × ≠⊸⥊)⎉1
WriteFull ∾≍ Stereo∘((lm×⊑)(+⎉1´⥊⎉1¨)1⊸↓)¨ ⟨
  ⟨4,  k7, {((×˜×3-2⊸×)↕⊸÷≠𝕩)×𝕩}(∾˜lm↑a)∾a⟩
  ⟨4,  k , (3 Rep lm↑a)∾lm↓ax, (¯4×lm)↑˘(-lm)↑˘c⟩
  ⟨4,  k , ax, j, ∾≍(4‿4‿8×lb)↑˘¨<c, (4×lm)↑˘y⟩
  ⟨4,  k , ax, lm↓b, (4×lm)↑˘j, (I 4×lm) FA ss⟩
  ⟨4,  k , ax, 0⊑be, (⌽I 4×lm) FA ss⟩
  ⟨4,  k , ax, 1⊑be, c⟩
  ⟨8,  k , ax, ∾2‿3⊏be, c, (⌽I 8×lm) FA s, ((4×lm)↑(lm⥊0)∾⊢)˘y⟩
  ⟨4,  k9, a⟩
  ⟨6,  k , ax, b, c, (I 8×lm) FA s, j, ((22×lb)↑(lm⥊0)∾⊢)˘z⟩
  ⟨8,  k , ax, b, lm↑˘c, (∨˜I 8×lm) FA (0.1×SSaw(5×lm)⥊0.7 Trans Note"d3") (5×lm)_crossfade (8×lm)⥊p⟩
  ⟨8,  k9,     b,            (¬(2÷3)××˜I 8×lm) FA p⟩
  ⟨7÷8,k7⟩
⟩
