#! ./run_once

lb ← ⌊⌾(÷⟜120)0.37×fr
Out ↩ (0.4×max)⊸×
root ← Note "A2"
Tr ← Trans⟜root
sc ← (⥊»+⌜˜(↕7)×⊢´)+`2‿1‿3‿1
# 0 2 3 6 7 9 10 13 14 16 17 20 21 23 24 27 28 30 31 34 35 37 38 41 42 44 45 48

co← 0.6 × 800‿1.5 Lp2q ∾ ((+´÷≠) (lb×⊑) Saw∘⥊¨ ·Tr 24+1⊸↓)¨    ⟨5‿ 0‿4‿7, 3‿ 3‿7‿10, 5‿ 2‿5‿9‿10,           3‿ ¯4‿1‿5‿8⟩
c ← 0.7 × ∾ ((+´÷≠) (lb×⊑) Sine∘⥊¨ ·Tr ·∊⟜sc⊸/·⥊(12×↕5)+⌜1⊸↓)¨ ⟨5‿ 0‿4‿7, 3‿ 3‿7‿10, 4‿ 2‿5‿9‿10, 2‿ 2‿6‿8‿11, 2‿ 1‿5‿8⟩
cm← 0.7 × ∾ ((+´÷≠) (lb×⊑) Sine∘⥊¨ ·Tr ·∊⟜sc⊸/·⥊(12×↕5)+⌜1⊸↓)¨ ⟨4‿ 0‿3‿7,                         2‿ 2‿6‿8‿11, 2‿ 1‿5‿8⟩

_st ← {(÷⟜2×2+((1+2⋆-𝕗)⋆-𝕩)×(1-×˜))Triangle𝕩}

p ← 0.5 × (0⥊˜lb÷2)»((3 _st+0.2×Square)∘/×200 Lp1 2⋆¯3e¯4×·⊒∘/⊣¨)´ ⟨(lb÷2)⊸×,Tr⟩{𝕎𝕩}¨⟨
   1‿ 1‿ 2‿ 2‿ 1  ‿ 1‿ 1‿ 1‿ 2‿ 1‿ 3 ‿  1‿ 1‿ 2‿ 1‿ 1‿ 1‿ 1  ‿ 1‿ 2‿ 5
  24‿26‿27‿31‿30.3‿29‿24‿26‿27‿29‿22 ‿ 19‿21‿22‿27‿26‿24‿22.2‿24‿19‿17
⟩
Mg← {0.2 × {(≠⥊𝕨˙)⊸×𝕩}˜´⌽⟨|0.3-4⋆˜I lb÷2, ¬0.5×·Cos 0.2-˜(2×π)×I 2×lb, (2×lb) (Square+0.4×Saw)∘/ Tr 𝕩⟩}
⟨m0,m1⟩ ← Mg¨ ⋈⟜(18⌾(¯1⊸⊑)) 2/12‿8‿15‿17

d ← 0.4 × 0.6  Pan (lb÷2) (10  _st∘/×500 Lp1 0.02+2⋆¯5e¯4×·⊒∘/⊣¨) Tr 48‿45‿42‿37‿35‿30‿28‿23‿20‿16‿14‿9‿7‿3‿2‿0
Sg← 0.5 × 0.47 Pan (lb÷4) (7.5 _st∘/× 95 Lp2      2⋆¯7e¯4×·⊒∘/⊣¨) Tr
s ← Sg sn ← ∾⟨
  34‿35‿37‿38‿13‿41‿45‿27‿23‿21‿20‿23‿35‿23‿14‿28
  34‿35‿37‿38‿37‿41‿45‿44‿24‿23‿24‿28‿27‿23‿24‿31
⟩
sz← Sg (9↑sn) ∾ 1↓⌽⥊(7×↕6)+⌜3×↕4

Yg← {0.5 × 4‿root‿2 Peak (lb÷𝕩÷2) (Triangle∘/× 1e3 Lp2 0.3⌈2⋆¯8e¯4×·⊒∘/⊣¨) Tr 𝕨+(𝕩↑1↓sc)∾⌽⥊(7×↕𝕩÷4)+⌜3×↕4}
y ← 0.51 Pan            7 Yg  8
z ← 0.55 Pan (-lb÷16) ⌽ 0 Yg 16

t ← (-2×lb)↑(∾˜∾0¨) t0 ← 0.6 × 500 Hp2 (lb÷3)↑/⁼0⌈(↕∘≠+⌊) 10×(Sine 155⊸×)⊸× 2⋆¯3e¯3×↕lb÷2
k0← 0.4×1(-÷+)⋆3××˜∘⌽∘I∘≠⊸×(+´-×´)Sine∘×⟜(1+0.19÷4⋆I 11e3)¨72‿162‿433
ka← ∾ ↑⟜k0¨ (lb÷2)×(≠(«-⊢)/)'.'=".   . . .   ... "
k ← ka + (¯8×lb)↑(0⥊˜1.5×lb)∾˜0.7×1e3 Lp1 500 Hp2 2 Rep (lb÷8)↑k0
kl← (400⊸Lp2 + 3e3⊸Hp2) ka

WriteFull ∾≍ Stereo¨ (((8×lb)×⊑)(+⎉1´⥊⎉1¨)1⊸↓)¨ ⟨
  ⟨4, co,0¨⊸∾p⟩
  ⟨4, co,m0∾p⟩
  ⟨8, co,m1∾p,kl⟩
  ⟨2, co⟩
  ⟨4, (+´⥊¨⟜co‿c× ·¬⊸⋈·(1⌊5××˜∘¬⊸×)2×I) 32×lb, d⟩
  ⟨4, c,d,t⟩
  ⟨4, c,d,t,s⟩
  ⟨4, c,d,t,s,k⟩
  ⟨1, c,d,((4×lb)↑(0⥊˜lb÷2)∾4 Rep(⌊lb÷3.7)↑t0)∾2 Rep t,((4×lb)↑˘sz)∾˘(4×lb)↓˘s,k⟩
  ⟨2, (8×lb)⌽c,d,t,s,k⟩
  ⟨1, (8×lb)↓c,d,(8×lb)↑t,sz,k⟩
  ⟨1, cm⟩
  ⟨2, k,cm,(¬0.4×¬I 5×lb)Fadefront(0 Yg 8)∾○(∾˜)⎉1 y⟩
  ⟨1, k,cm,y,z⟩
  ⟨3, k,cm,y,z,(4×lb)↓˘s⟩
  ⟨1, k,   y,z÷2⟩
  ⟨3, c,d,(3 Rep t)∾(lb-⊸↑t0∾0⥊˜⌊0.3×lb)∾lb↓t,s,k⟩
  ⟨1, (8×lb)↓c,d,t,sz,k⟩
  ⟨3, c,d,t,s,k,∾⟜(0¨)∘∾˜˘0.8×y+z÷2⟩
  ⟨1, (8×lb)↓c,d,(8×lb)↑∾˜t,sz,k⟩
  ⟨4, (¬×˜I 24×lb) Fadeback ∾˜ (+´⥊¨⟜c‿co× ·¬⊸⋈·(5××˜∘¬⊸×)I) 16×lb, d⟩
⟩
