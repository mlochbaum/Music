#! ./run_once

lb←⌊⌾(÷⟜600)0.46×fr
factorCounts ← •Import "factc.bqn"
fa ← 10 Hp2 10 ÷˜ FactorCounts lb÷5
Poly ← {+⟜(𝕩⊸×)´𝕨}

Kg← {1(-÷+)⋆(1600÷𝕩)× 23‿0.81 Hp2q⍟16 ((2⋆÷⟜𝕩) × ·Triangle 0⌈43⊸+) ¯25÷˜↕lb}

top ← fr÷32×2  # F#4 plus ~23 cents
w ← ∾ 7‿1‿0‿6‿1‿1/∾˜ ((4×lb)↑/⟜fa)¨ 32‿36‿27
l ← (4×lb) ↑ Kg 2
n0← ∾˘˝ 1‿0.9 × 0.41‿0.36 Pan⟜(23⥊∘↑⊢)˘ > (lb÷4) ((0.4×0.6⋆I∘⊣) × Square∘⥊)¨ (0‿¯2‿0⊸∾˘¯5‿¯7) Trans top
n ← (12.4×lb) ↑˘ ¯12‿7e3‿2 Peak 0.7‿0.3‿0.1 +´∘× (-⌊lb÷2.05)⌽˘⍟(↕3) n0

map←". ··˙   .   ˙·· .  ·˙  ·. · ˙ · "
j ← (2×lb)↑Kg 31
k ← (map='·') (lb÷4){(⌊𝕗×0.07+(≠-¯1⊑/)𝕨) ⌽ ∾ (𝕗×(≠|·-⟜(¯1⊸⌽)/)𝕨) ↑¨<𝕩 } Kg 1.4
s ← 0¨⊸∾ lb ↑ 10‿80‿1 Peak 9‿280‿1 Peak 4e3 Lp1 (××2.2√|) ((¯1+2|1.2⋆3+(9×⋆2)|⋆⟜(¬4.3e¯5))×2⋆÷⟜¯900)↕8e3
h ← ((2×lb)⌽(3 Rep (lb÷6)⊸↑)∾30⊸Rep) ((÷1+11×I) × (¯0.7+2|π⋆˜2÷˜↕) × Saw∘⥊⟜8278) lb÷4

b ← 70 Hp1 Clip ((¯2‿¯0.5‿10‿¯4 Poly 0.75 Saw⥊⟜(Note"F#1")) × 1+0.4×·Sine·-⟜(≠÷˜fr|+´) 2.5‿¯1‿20‿¯14 Poly I) 4×lb
fs← 1-˜(-∾⟜(⊢˝)⥊0‿19+⌜0‿7‿14+⌜0‿2‿0‿7‿5‿12‿10) Trans top
f ← 0¨⊸(∾∾∾˜)˘ 0.56 Pan 2×w √∘|⊸×○((16×lb)⊸↑) (1.4×top) Lp1 (lb÷4) ((0.4⋆·√I∘⊣) (≠⊸⥊˜×⊢) ·•math.Sin 2.8×Saw∘/) fs

Out ↩ max × 3 Softclip 0.3×20⊸Hp2
(64×lb) Write ⟨w,n,j,k,s,h,b,f⟩

song ← (64×lb) (+⎉1´⥊⎉1¨)¨ ⟨
  ⟨w,  l⟩
  ⟨w,n,l⟩
  ⟨w,n,l,0.3×+´((1(-≍+)<)×≠⥊¨h‿k˙)•math.Cos(3.3×π)×I-64×lb⟩
  ⟨w,  l∾○((32×lb)⊸⥊)j,k,s⟩
  ⟨w,  j,k,s,h,b⟩
  ⟨w,n,j,k,s,h,b,f⟩
  ⟨w,n,j,k,s,  b⟩
⟩
"g.wav" wav.Write˜ Out ∾≍ Stereo¨ song
