#! ./run_once

lb ← ⌊⌾(÷⟜120)0.4×fr
Tr ← (Note "F#1") × 4 ⋆ ÷⟜17
Env ← ·⊒∘/⊣¨
_st ← {c←(1+2⋆-𝕗)⋆-𝕩⋄(1+c÷2)×(÷⟜2×2+c×(1-×˜))Triangle𝕩}

SW ← (5 Hp1 20 Lp1 5×Pink÷√)⊸×⊸{+´(⊢÷˜·Sine𝕩⊸×+𝕨×√)¨(1+4×↕7)}{(√𝕘)÷˜·+´·𝔽¨𝕘⥊<}4
co ← 2e3
Cg ← 0.2 × ·+´ (co+(2×lb)×⊑) SW∘⥊¨ ·Tr 1⊸↓
Cp ← ∾ co{(𝕗↓𝕨)𝕗_crossfade𝕗↑𝕩}¨⟜(1⊸⌽)
Cl ← +´co-˜≠¨
c ← Cg¨ ⟨
  1‿ 1‿3‿6‿11, 1‿ 3‿8‿11, 1‿ 1‿2‿8‿13, 1‿ 1‿3‿8, 2‿ 0‿3‿5, 2‿ 2‿5‿8
⟩
cr ← (∾˜¯2↑c)∾c
cs ← c ∾˜ (¯1↓c) ∾ Cg¨ ⟨ 2‿ 1‿3‿6
  2‿ 3‿5‿10, 2‿ 3‿6‿11, 1‿ 0‿3‿5, 1‿ 0‿3‿5‿10, 2‿ 3‿6‿11
  2.7‿ 3‿8‿13, 1‿ 3‿8‿12, 2.3‿ 0‿3‿5‿10, 2‿ 3‿6‿11
  1‿ 3‿8‿13, 3‿ 3‿8‿12, 1‿ 0‿3‿5, 1‿ 0‿3‿5‿10, 2‿ 3‿6‿11
  1‿ 3‿8‿13, 3‿ 3‿8‿12, 2‿ 0‿3‿5, 2‿ 2‿5‿8
⟩

Sg ← {0.5 × (5 _st∘/× 95 Lp2 7 _st∘(50+/)(⊢×1+·×˜3÷˜×)2⋆𝕨×Env)´ ⟨(lb÷4)⊸×, Tr 17⊸+⟩{𝕎𝕩}¨𝕩}
m ← 0.53 PanAP (120⥊0)⊸»⊸+ ¯2e¯4 Sg ⟨
  24‿ 4‿ 4‿ 8‿12‿ 4‿ 4‿ 4
  17‿17‿16‿13‿15‿15‿17‿16
⟩
u ← 0.52 PanAP lb-⊸⌽ ¯3e¯4 Sg ⟨
  1‿1‿1‿1‿ 2‿ 1‿ 1‿2‿1‿1‿1‿ 1‿18 ‿ 1‿1‿1‿3‿1‿1‿1‿ 1‿1‿1‿20
  0‿2‿3‿8‿11‿13‿10‿8‿8‿3‿8‿11‿ 3 ‿ 0‿3‿5‿8‿1‿3‿8‿10‿8‿5‿ 8
⟩

_rEnv ← {(500⊸⌽⊸≥÷1+𝕗×⊢)∘Env}
e ← 0.44 PanAP 7‿700 Hshelf 0.2×1(-÷+)⋆4× (1-·|(930⥊0)⊸»⊸-)⊸× (lb÷4) (Sine∘/×200 Lp1 2e¯4 _rEnv) Tr 13‿14‿13‿8‿13‿14‿13‿5‿10‿13‿14‿16‿17‿13‿14‿16
Hg ← (××1-˜·√1+×˜)⌾(2⊸×) Sine∘/ × 0.2 + 11 _st∘(30+/)(⊢×1+·×˜2÷˜×)200 Lp1 5e¯4 _rEnv
h ← 0.48 PanAP ¯700 ⌽ (4×lb÷5) Hg Tr 13‿14‿13‿8‿9‿ 11‿13‿14‿11‿9‿ 11‿8‿6‿8‿11‿ 10‿8‿13‿10‿11

s0 ← 0.15 × ¯4‿1500‿1.5 Peak 8‿600‿0.8 Peak ((-⟜¬1|·+`π|4÷˜↕) × ·(5⊸×⌊·÷1⊸+)3e¯4×↕) 3e4
LS ← {m←¯1⊸«⊸≠𝕩⋄⥊⍉[-⟜(¯1⊸»)/m,m/𝕩]}
SS ← {l𝕊[v,k]: (⋈⟜1e4⊸HShelf⟜s0¨ -10+v) (∾·0¨⌾⊑↑¨⟜<˜)¨ ⌊∘×⟜(𝕨÷+´)¨ LS⍟k ⋈1}
q ← ∾○(+´)⟜(¯1⊸↓) (8×lb) SS 2‿6‿7‿5≍1‿3‿7‿5
r ← 0¨⊸∾(0⥊˜1.3×lb)⊸Fadeback⊸∾ +´ (4×lb) SS 5‿9≍2‿3
s ← ∾⟜(≠↑(0⥊˜1.7×lb)Fadefront·∾(6‿¯1×lb)↑¨<) +´ (8×lb) SS 9‿8‿5≍2‿4‿7

(16×lb) Write ⟨Cp c,s,m,u,e⟩

lm ← 16×lb
In ← ×⟜lm (⥊×3√I∘⊣)⎉1 ⊢
WriteFull ((4×lb)↑˘m) ∾˘˜ ((∾∘≍ Stereo∘((lm×⊑)(+⎉1´⥊⎉1¨)·∾⟜0⍟(0=≠)2⊸↓)¨) +⎉1 ·Cp·∾((lm×⊑)(÷⟜Cl Rep ⊢)1⊸⊑)¨) ⟨
  ⟨2, cr⟩
  ⟨2, c,(0⥊˜3×lb) Fadeback m⟩
  ⟨4, 4⌽cr,m,u,q⟩
  ⟨1, c⟩
  ⟨6, cs, 6 In h⟩
  ⟨1, c,m,h⟩
  ⟨2, c,m,h,r⟩
  ⟨2, cr, ((2×lm)↑(6×lb)↑⊢)˘m⟩
  ⟨2, c,m,u⟩
  ⟨4, c,m,u,s⟩
  ⟨8, c,m,u,s,8 In e⟩
  ⟨1, c,m⟩
⟩
