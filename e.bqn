#! ./run_once

Out ↩ max × 3 Softclip ⊢
l0←0.40×fr
l1←⌊⌾(÷⟜300)0.36×fr
lb←l0

Transm ← (5√1+2÷7)⊸⋆⊸×
base ← Note "A2"

bass ← (2⋆24) ÷˜ ⊏ ¯1⊑Read "bass"
b ← 2÷˜4‿800‿1 Peak 1(-÷+)⋆-8× 5‿70‿2 Peak 12e3 Lp1 1950‿50 Notch ¯5‿2600‿1.3 Peak ¯3‿200‿1 Peak 26 Hp2 bass
# (bass tab)
# 3->2---------------3--5---------2->3--------------------
# ------3-5|------1-------3|------------3-5|-1-|0|--------
#         ⋆       ×       ⋆          ×    ⋆  ×

k ← lb ↑ 1.2 × ((0.5×Pink×(⊢×2⋆¯4⊸×)∘I∘≠)1e3⥊90) Add ((Sine×2⋆¯4×I∘≠)2e3⥊91) Add ((2⋆¯10×I) × (×˜⊏⊸÷0.8+↕6) +˝∘× Sine∘⥊˘⟜(3+41×↕6)) lb
v ← 0.58 PanAP ∾ 0.3‿0.34 × (¯700+0.5‿0.75×lb) ((-lb)↑↑)¨< (+˝ ((50‿130‿300÷˜⌜↕) ⌊⎉1 0.05⋆I) × Sine∘⥊˘⟜(Note¨"acb")) 0.5×lb
hl← ⌊(1‿0.5≍1.88‿0.62)×lb÷2
Hg← ((3200÷˜3√70+×˜↕400)×⌜↕⌈´⥊hl){(+˝÷√∘≠)(0.1×1-2|3400×𝕗)×(⋆⌊⊢)⟜𝕗}
h ← 0.43 PanAP 3‿8000 Hshelf ∾⥊ hl ↑¨ 400{(𝕗⊸↑) 𝕗_crossfade 5500⊸Hp1}¨(1.3×5‿7200‿1.3⊸Peak)⌾(1⊸⊑)Hg¨0.97‿0.88
y ← 0.4  PanAP ∾0‿0‿0‿1‿0‿0‿1×< (2×lb) ↑ 0.15 × (((√I 500)Fadefront 0.08⋆I) × · +´ Sine∘⥊¨⟜(2×Note¨"ga")) 0.6×lb
e ← 0.2 × (1.3+·Sine 12+7×·Sine 4¨)⊸× ∾ ¯1 (((¯4×lb)↑¨↓) ∾ ·(((6×lb)↑(0⥊˜2×lb)∾∾˜)(lb×2)↑(I-1e3)Fadeback(lb÷2)⊸↑)¨↑) lb (Sine(-÷2˙)Triangle)∘⥊¨ 2‿4‿8÷˜Note¨"aeg"

cl ← 0.1 × 8‿400‿1 Peak ((Sin ·+`(2×π)|8÷˜↕) × 1⌊1.2×2⋆¯1.3e¯3×↕) 9e3
GenS ← {𝕩‿1e4 HShelf cl}
s ← 0.52 PanAP (∾ 0¨⌾⊑(⌊l0×3.1‿3‿5.15‿4.75÷4)↑¨< GenS ¯12) + (0¨∾3⊸Rep) l0 ↑ 1.3 × GenS ¯8

map←"*--x--yxy--x-+--*---y-+x-x---+-x*--y-x--yx---x+--*---xy--x-+x-+-"
Kg← {4÷˜1(-÷+)⋆(1200÷𝕩)× 37‿0.8 Hp2q⍟15 ((2⋆÷⟜𝕩) × ·Triangle 0⌈43⊸+) ¯25÷˜↕l1}
ks ← (Kg¨ 9‿1) ∾ 1.3‿1 × (0.52 PanAP GenS)¨ ¯8‿¯14
kp ← +´ Stereo¨ (map⊸=¨"*+xy") (l1÷4){(⌊𝕗×(≠-¯1⊑/)𝕨) ⌽ ∾ (𝕗×(≠|·-⟜(¯1⊸⌽)/)𝕨) ↑¨<𝕩 }⎉1¨ ks

_declip ← {1⌊·|1-˜𝕗÷˜(↕𝕗)⊸«}
_g0 ← {
  h‿g ← (<∘⍋∘⊑ ⊏¨ ⊢) ⥊¨ ⟨𝕩×⌜𝕨, 𝕩⊢⌜0.6⋆𝕨⟩
  o ← ⍋2|1+0.3×h
  g ×⟜(𝔽¨)○(×`o⊏⊢÷1⊸») h
}
Gen ← {(3×1.5√100÷𝕩)×(≠𝕨)÷˜+´(1+↕8) (0.3 Sine ·((fr÷2)⊸>⊸×)𝕩⊸×)_g0 𝕨}
cn← ⟨2‿3‿4÷3‿5‿7,3‿5‿5÷4‿6‿9,2‿4÷3‿5,4‿5‿8÷9‿7‿11⟩ ⋈¨ 0‿3‿5‿2.4 Transm 2×base
c ← (1e3 Lp2 0<·Square 7+3×·Sine 3¨)⊸× 0.16 × (4×lb) ((80 _declip ·⊒∘/⊣¨) × ·∾(⊑∘⊢Gen⥊⟜(⊢´))¨) cn

Env ← 50 _declip × 0.3⋆1.4√÷⟜lb
_sy ← {(Env·⊒∘/⊣¨) × 𝔽∘/}
Genb← {(0.4×1.5√100÷𝕩)×+´(0.6⊸⋆×·(Sine ·((fr÷2)⊸>⊸×)𝕩⊸×)¨1+(2÷7)⊸×)-⟜⊑∧⍷⥊×⌜˜2+↕6}
m ← 4÷˜ (2÷˜lb×1‿1‿1‿2‿1‿1‿2‿1‿1‿2‿3) Sine _sy (15+7‿7.8‿7‿1‿3.6‿5‿0‿2‿7‿7.7‿10.5) Transm base
bp← (∾1‿1‿4/(⊑-·+´1⊸↓)⊸«¨⌊lb×⟨2,÷2.5⟩‿⟨2⟩‿(1∾3‿3÷8)) Genb _sy (9‿1‿4∾∾˜1‿0‿7‿8‿2‿6.4) Transm base

(14×l0) Write ⟨k,v,y,h,b,e,s⟩
#(16×l1) Write ⟨kp,c,m,h,e,y,0¨⊸∾bp⟩
