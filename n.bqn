#! ./run_once

lm←16×lb←⌊⌾(÷⟜300)0.38×fr
root ← Note "Bb2"

HL ← {h‿l𝕊𝕩: h Hp1 l Lp1 𝕩}
w ← 1.4e4 Lp2 ¯2‿3000 HShelf 20÷˜+˝(0.8+0.06×¯7e3‿8e4⌽⎉0‿1 Sine (lm÷2)⥊2×fr÷lm) ×⟜(2⊸Rep)⎉1 +˝ (Square (1+6‿10 HL 0.3×Pink)⊸×)⎉1 1‿6‿2‿1/(2⋆2-˜↕4)×⌜(4×lb)⥊˘22‿7 Trans root
c ← lm ⥊ lb ↑ 2e3 Lp2 ⋆-I 200
a ← ¯400 ⌽ ∾ 0‿0.96‿0.83 × (3‿3‿2×lb÷2) ↑¨< ×⟜(0.02+40⋆·-·I ¯1⊑≢) Sine (2×lb)⥊22 Trans root
j ← lm⥊ (-lm÷2) ↑ (4×lb) ↑ (⌊¯2.26×lb) Shift (500-˜lb÷2)⊸↑⊸∾ 0.2 × ¯1⌈1⌊ 120÷˜ ⌊⌾(64⊸×) (⌊2÷˜≠)⊸⌽ (⋆∘(2÷˜×˜)∘×⟜4 × ·Saw(17 Trans root)¨) (¯1↓⌽)⊸∾I 5e3

Ug← {2.6÷˜4e3 Lp1 (I 400) Fadefront (I ⌊¯0.14×lm) Fadeback (1.3-0.3××˜)⊸× Saw (⊏⥊˜lm-≠)⊸∾  (⊑+((×˜×3-2⊸×)⍟5 I 0.9×lm)×-˜´) 𝕩 Trans root÷4}
u ← ∾us← Ug¨⟨0‿0,7‿12,10‿7,8‿5⟩
v ← ∾0¨¨⌾(0‿1‿3⊸⊏)us

map←". ˙. ˙ .˙  ˙···˙"
Arr ← (lb÷2){ (-⌊𝕗×⊑/𝕨) ⌽ ∾(𝕗×(≠|1⊸⌽⊸-∘/)𝕨)↑¨𝕩 }
Kg← {((2⋆(-18÷fr)×↕∘≠)×16 Hp2 0.2 Sine 30⋆1+|)⍟𝕩 lb⥊0}
k ← +˝ ("˙·."=⌜map) Arr˘ (⌊lb÷5)⊸(⌊∘÷⟜7⊸⥊⟜0∾↑)⊸∾⌾⊑ 0.6×Kg 2‿6‿18

Ds← (1.1≍˜2×·⊑⊢) Lp2q ((1⌊500÷˜⌽)÷2+(4.3÷lb)⊸×)∘↕∘⊣ × Square∘⥊
d ← ∾ (3‿4‿5‿2‿2‿1‿1‿3‿3‿6‿1‿1×lb÷2) Ds¨ (8‿3‿7‿0∾12-˜0‿7‿12‿5‿7‿8‿5‿13) Trans root
r ← 0.6 × (-lm)↑ ≠⊸Ds (19.3-11×3√I 2×lb) Trans root
Bg ← { lb ⌽ (∾˜(>⟨1‿2‿5,1‿3.6‿3.4⟩)×lb÷4) 𝕏○⥊ (>3‿1/8‿7⊸∾¨8‿0) Trans root÷2 }
b ← Bg⊑⟨∾Ds¨⟩
Dd← ·+˝ (4∾2⋆-/3‿4‿1‿1) Saw∘(⊢×1+2×3 Hp2 Pink÷√)∘×⎉0‿1 /
bd← Bg⊑⟨0.5 × 2 Softclip (÷4+(6÷lb)⊸×)∘⊒∘/∘⊣ × Dd⟩

s0← 0¨⊸∾(2×lb)↑ (××1.5√|) ¯12‿4000‿0.8 HShelf ((¯1+2|⊢⋆1.3+÷⟜1e5)×2⋆÷⟜¯1700)↕⌊lb÷2.5
s ← ∾ (0‿2‿1‿2×-lb÷2) ⌽¨ < s0

Out ↩ (max÷3)⊸×
(4×lm) Write ⟨k,d,s,bd,∾0‿1‿0‿0‿0‿1‿1‿0⊏0¨⊸⋈a⟩

WriteFull ∾ ((lm×⊑)(+⎉1´⥊⎉1¨)1⊸↓)¨ ⟨
  ⟨1, w⟩
  ⟨2, w,c,a,0¨⊸∾⍟2 k⟩
  ⟨4, w,c,a,j,v⟩
  ⟨2, w,c,a,j,v,k,0¨⊸∾s0⟩
  ⟨1, 2⊑us⟩
  ⟨1, b⟩
  ⟨2, k,0¨⊸∾⍟2 s0,b⟩
  ⟨2, k,s0,b⟩
  ⟨2, k,s0,(I∘-×⥊⟜b)2×lm⟩
  ⟨1, k,d⟩
  ⟨1, k,d,s⟩
  ⟨4, k,d,s,r,I∘≠⊸×(4×lm)⥊w⟩
  ⟨4, k,d,s,r,w,u⟩
  ⟨4, k,d,s,r,w,u,bd,∾0‿1‿0‿0‿0‿1‿1‿0⊏0¨⊸⋈a⟩
  ⟨2, k,d,s,bd      ,∾0‿1‿0‿0⊏0¨⊸⋈a⟩
  ⟨2, k,d,b,∾0‿1‿0‿0⊏0¨⊸⋈a⟩
  ⟨2, k,b,∾0‿1‿0‿0⊏0¨⊸⋈a⟩
⟩
