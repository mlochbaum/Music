#! ./run_once

lb←⌊⌾(÷⟜300)0.36×fr
root ← Note "Bb2"
Out ↩ (max÷3)⊸×

map←". ˙. ˙ .˙  ˙···˙"
Arr ← (lb÷2){ (-⌊𝕗×⊑/𝕨) ⌽ ∾(𝕗×(≠|1⊸⌽⊸-∘/)𝕨)↑¨𝕩 }
Kg← {((2⋆(-18÷fr)×↕∘≠)×16 Hp2 0.2 Sine 30⋆1+|)⍟𝕩 lb⥊0}
k ← +˝ ("˙·."=⌜map) Arr˘ (⌊lb÷5)⊸(⌊∘÷⟜7⊸⥊⟜0∾↑)⊸∾⌾⊑ 0.6×Kg 2‿6‿18

Ds← (1.1≍˜2×·⊑⊢) Lp2q ((1⌊500÷˜⌽)÷2+(4.3÷lb)⊸×)∘↕∘⊣ × Square∘⥊
d ← ∾ (3‿4‿5‿2‿2‿1‿1‿3‿3‿6‿1‿1×lb÷2) Ds¨ (8‿3‿7‿0∾12-˜0‿7‿12‿5‿7‿8‿5‿13) Trans root
r ← 0.6 × (-16×lb)↑ ≠⊸Ds (19.3-11×3√I 2×lb) Trans root
Bg ← { lb ⌽ (∾˜(>⟨1‿2‿5,1‿3.6‿3.4⟩)×lb÷4) 𝕏○⥊ (>3‿1/8‿7⊸∾¨8‿0) Trans root÷2 }
b ← Bg⊑⟨∾Ds¨⟩
Dd← ·+˝ (4∾2⋆-/3‿4‿1‿1) Saw∘(⊢×1+2×3 Hp2 Pink÷√)∘×⎉0‿1 /
bd← Bg⊑⟨0.5 × 2 Softclip (÷4+(6÷lb)⊸×)∘⊒∘/∘⊣ × Dd⟩

s0← 0¨⊸∾(2×lb)↑ (××1.5√|) ¯12‿4000‿0.8 HShelf ((¯1+2|⊢⋆1.3+÷⟜1e5)×2⋆÷⟜¯1700)↕⌊lb÷2.5
s ← ∾ (0‿2‿1‿2×-lb÷2) ⌽¨ < s0

(16×lb) Write ⟨k,d,r,s,bd⟩

WriteFull ∾ (32×lb)⊸((+⎉1´⥊⎉1¨)¨) ⟨
  ⟨b⟩
  ⟨k,0¨⊸∾⍟2 s0,b⟩
  ⟨k,s0,b⟩
  ⟨k,s0,(I∘-×⥊⟜b)32×lb⟩
  ⟨k,d⟩
  ⟨k,d,s⟩
  ⟨k,d,s,r⟩
  ⟨k,d,s,r⟩
  ⟨k,d,s⟩
  ⟨k,d,s,bd⟩
  ⟨k,d,s,bd,r⟩
  ⟨k,d,b⟩
  ⟨k,b⟩
⟩
