#! ./run_once

lb ← ⌊⌾(÷⟜12)0.74×fr

SS ← {+˝ Saw∘(⊢×1+3 Hp1 10 Lp1 3×Pink÷√)˘ (2⋆-/𝕨)×⌜𝕩}
ao ← (∾"0a"+⟜↕¨10‿26)⊸⊐"8565bcg4h6656ja1"
an← (Note"f0")×(2⋆·⌊2⊸×)⊸× ao Trans 1
Fall ← {(⌊´(⊢+(I(lb×𝕩÷4)×≠an)×-)⌾(⋆⁼)⌈´)an}
ae← ∾ (1-˜1∾2⋆↕5) ((Rep(⌈÷4⋆<)Fall∘⊣) ∾ ⊢)¨< (lb÷4)/an
a ← 0.2 × ((√¬I lb÷8)⥊˜≢)⊸× 2 Softclip 10× 3‿1‿2‿1 SS ae

ST ← {k‿e𝕊n: (Sine⊸× k × ⋆(-e)×↕lb) × Triangle lb⥊n}
TR ← {a‿b𝕊𝕩: (a-b×·|(1|π⋆˜↕∘≠)×-⟜»)⊸× 𝕩}
k ← (2×lb) ↑ 3 Softclip 0.5 × 1‿1.6 TR 120‿2e¯3 ST 110
Sg← {Clip 60 Hp2 0.25‿6 TR 200‿1.3e¯3 ST 𝕩÷˜Note "d3"}
s ← 0¨⊸((3 Rep ∾)∾((⌊lb×0.36)↑0.5×Sg 3)⊸«⊸∾) 0.7 × Sg 1

kw← (⊢+>○|×-)´ 6e3‿260 {l‿w𝕊𝕩:(100×0.5-𝕩)××˜∘¬⊸×0⌈1⌊w÷˜(<↕l+w)-l×»𝕩} (3.41××˜∘¬⊸×)⍟(↕30) 0.002
kz← ∾3‿1‿2‿2 (lb-⊸↑(lb÷2)↑Rep)¨< (⌊lb÷7) ↑ 700 Hp1 kw

j ← (-lb÷4) ⌽ ∾ 0¨⌾(¯1⊸⊑) 4 Rep (⌊(lb÷2) × ∾⟜(4-+´) 1‿1.27) ↑¨< 1 Softclip 15 Hp2 kw

r ← (2×lb) ↑ ¯4‿900‿1.2 Peak 12‿500‿0.5 Peak 5 Hp2 3e3 Lp2 ((÷⟜8⌊2⋆÷⟜¯1e3)×1-2|1.2⋆(7×⋆2)|⊢)↕4e3

h ← 7500 Hp1 ∾(6‿4‿7‿3÷4)×(⌊+`⁼lb×1∾˜(2‿1‿2÷70)+1↓↕⊸÷4)↑¨<{(+˝÷4×√∘≠)(¬2|𝕩)×0.6(⋆⌊⊢)𝕩÷8e3}(3√20+×˜↕200)×⌜↕lb÷2

ks ← lb⌽(lb×4)↑0.5×((lb÷2)↑(⊢∾÷⟜2)(⌊0.38×lb)↑k)∾0.7×Sg 1

Gb ← { ¯6‿1000 Hshelf ∾ (⌊lb×𝕨) (Pluck⋈˜∾1e3÷⊢)¨ 𝕩 Trans Note"Bb1" }
b ← 1.2 × 80 Hp1 180 Lp1 Gb´ ⟨5‿10.5‿2‿7.5‿7÷8, 0‿7‿8‿5‿¯2⟩

t ← 0.6 × 300 Hp1 ((1.2‿0.8÷4)⥊˜≠)⊸Gb 24+ao
Fg← {0.3 × ¯8‿900‿0.8 Peak 2 Softclip 4× ((¬⋆6˙)⊸×I∘≠)⊸× 4‿1‿0‿1 SS ⌽⍟(𝕨⊣0) 4÷˜Fall𝕩}
⟨f1,f2,f4⟩ ← Fg¨ 1‿2‿4

(16×lb) Write ⟨j,r,h,b⟩

lm ← 16×lb
s0 ← ∾ ((lm×⊑)(+´⥊¨)1⊸↓)¨ ⟨
  ⟨2, j,r,h,b,   ((¯2×lb)⌽lm↑1 Fg 1.5) ∾ (¯5×lb)⌽lm↑f1⟩
  ⟨1, j,r,h,b,t⟩
  ⟨1, j,r,h,b,   (¯7×lb)⌽lm↑f2⟩
  ⟨÷4,j,r,h,    ks⟩
  ⟨2, j,r,h,b,t,ks⟩
⟩
WriteFull ((¬×˜I lm) Fadeback s0) ∾ a + ∾ ((lm×⊑)(+´⥊¨)1⊸↓)¨ ⟨
  ⟨3, k,s⟩
  ⟨1, k,s,lb(↑∾3 Rep-⊸↑)kz⟩
  ⟨2, k,s⟩
  ⟨1, k,s,((2×lb)⥊0) Fadeback kz⟩
  ⟨1, k,s,kz⟩
⟩
