#! ./run_once

lb ← ⌊⌾(÷⟜12)0.74×fr

kw← (⊢+>○|×-)´ 6e3‿260 {l‿w𝕊𝕩:(100×0.5-𝕩)××˜∘¬⊸×0⌈1⌊w÷˜(<↕l+w)-l×»𝕩} (3.41××˜∘¬⊸×)⍟(↕30) 0.002
j ← (-lb÷4) ⌽ ∾ (3⊸Rep∾0¨⌾(¯1⊸⊑)) (⌊(lb÷2) × ∾⟜(4-+´) 1‿1.27) ↑¨< 1 Softclip 15 Hp2 kw

r ← (2×lb) ↑ ¯4‿900‿1.2 Peak 12‿500‿0.5 Peak 5 Hp2 3e3 Lp2 ((÷⟜8⌊2⋆÷⟜¯1e3)×1-2|1.2⋆(7×⋆2)|⊢)↕4e3

h ← 7500 Hp1 ∾(6‿4‿7‿3÷4)×(⌊+`⁼lb×1∾˜(2‿1‿2÷70)+1↓↕⊸÷4)↑¨<{(+˝÷4×√∘≠)(¬2|𝕩)×0.6(⋆⌊⊢)𝕩÷8e3}(3√20+×˜↕200)×⌜↕lb÷2

Gb ← { ¯6‿1000 Hshelf ∾ (⌊lb×𝕨) (Pluck⋈˜∾1e3÷⊢)¨ 𝕩 Trans Note"Bb1" }
b ← 1.2 × 80 Hp1 180 Lp1 Gb´ ⟨5‿10.5‿2‿7.5‿7÷8, 0‿7‿8‿5‿¯2⟩

ao ← (∾"0a"+⟜↕¨10‿26)⊸⊐"8565bcg4h6656ja1"
t ← 0.6 × 300 Hp1 ((1.2‿0.8÷4)⥊˜≠)⊸Gb 24+ao

an← (Note"f0")×(2⋆·⌊2⊸×)⊸× ao Trans 1
ae← ∾ (1-˜1∾2⋆↕5) ((⌈÷4⋆<)⟜((≍⟜¬∘I∘≠ +˝∘× ⌊´⋈⌈´)⌾(⋆⁼))∘Rep ∾ ⊢)¨< (lb÷4)/an
a ← 0.2 × ((√¬I lb÷8)⥊˜≢)⊸× 2 Softclip 10× +˝Saw∘(⊢×1+3 Hp1 10 Lp1 3×Pink÷√)˘ (2⋆-/3‿1‿2‿1)×⌜ae

ST ← {k‿e𝕊n: (Sine⊸× k × ⋆(-e)×↕lb) × Triangle lb⥊n}
TR ← {a‿b𝕊𝕩: (a-b×·|(1|π⋆˜↕∘≠)×-⟜»)⊸× 𝕩}
k ← (2×lb) ↑ 3 Softclip 0.5 × 1‿1.6 TR 120‿2e¯3 ST 110
Sg← {Clip 60 Hp2 0.25‿6 TR 200‿1.3e¯3 ST 𝕩÷˜Note "d3"}
s ← 0¨⊸((3 Rep ∾)∾((⌊lb×0.36)↑0.5×Sg 3)⊸«⊸∾) 0.7 × Sg 1

(8×lb) Write ⟨j,r,h,b,t⟩
