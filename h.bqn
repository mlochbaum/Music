#! ./run_once

lm←16×lb←⌊⌾(÷⟜100)0.78×fr

k1← lb ↑ 2e3 Lp1 ¯1.6×⋆-I 200
k ← ⥊(4≠↕8) ×⌜ k1
e0← (0.7-×˜)⊸× (¬0.7×1.4⋆˜I 0.8×lb) Fadeback ∾ ⟨⥊⟜0 , ((0.3×1+I) + (0.98⊸⋆)⊸⌊∘(500÷˜↕)) × Saw∘⥊⟜(Note"c")⟩ {𝕎𝕩}¨ -˜`0.4‿4×lb
e1← 0.7 × •math.Tan⁼ ¯5‿600‿2 Peak e0 •math.Tan⌾(÷⟜2)∘(⊢ + 0.1×-∘≠∘⊢↑≢⊸⥊⟜(1+0=↕)˜(∊∘/∘⊣×/)⊢)´ 1025+⌽⌈1.13⋆˜↕16
e ← ((1-0.7××˜I 2×lb)⊸Fadeback ∾ (-lb×0.3) ⌽ 2 Rep (2×lb)⊸↑⌾(1e3⊸⌽)) 8e3 Lp1 e1
r ← (0.5⥊˜lb÷2) Fadefront 0.4 × (-lb÷2)⌽((lb×0.25‿2) Slice e1) (≠∘⊣↑Reverb˜) 600 Hp1 0.4 × ÷⟜(+´⌾(×˜)) (1-˜2×1|1e8⊸×)⊸× {⋆-×˜1-˜(↕1+2×𝕩)÷𝕩} 1e4
Repmod ← ⊣ Rep¨ 32⊸÷⊸(⌊×)⟜⊑ ↑¨⟜< ·Sine(fr÷2)⥊1⊑⊢
y ← 3 ÷˜ 0.52 Pan 10 Hp2 6‿110‿4 Peak 90‿10 Notch 130‿10 Notch 3‿900‿0.9 Peak (4×lb)⊸↑⊸∾´ ∾∘Repmod⟜(⟨5÷4,3÷2⟩×Note"c2")¨ {(𝕩∾⌽1↓𝕩)⊸∾¨⟨3↓𝕩,∾˜𝕩⟩} 2⋆↕7
Tap ← (3⋆˜I∘-∘⊣) × Triangle∘⥊
d ← (lb×8)↑˘ ∾≍ 4⥊{⟨0⥊˜2≍1.97×lb,𝕩,⌽0.8×(0⥊˜lb÷2)Fadefront𝕩⟩} (0.4⊸Pan Add 0.4 × 0.7 Pan ¯9e3⊸⌽) 0.2 × ∾ (lb×0.47)Tap⟜Note¨"g"‿"f#"‿"d"‿"c#"
c ← (lb×8)↑˘ (⌊lb÷6) ⌽˘ ∾≍ 4⥊{⟨0⥊˜2≍1.97×lb,𝕨,⌽𝕩⟩}○(0.46 Pan 0.3 × ·∾(lb×0.47)Tap¨⊢)˝ 2×Note¨"g"‿"f#"‿"f#"‿"c#"≍"g"‿"e"‿"d"‿"c#"

b ← ∾ {(lm÷4) ⥊ (⌽0.7+0.3×·⋆¯2×I∘-∘≠)⊸× (I 30) (⊣Fadefront⌽⊸Fadeback) -⟜(+´÷≠) ×˜(0.01×1+I∘-∘≠)⊸+ Saw (lb÷2)⥊Note𝕩∾"1"}¨ "bbac"
l ← lb ↑ 0.6 × ⌊∘+⟜0.5⌾(64⊸×) (0.4×12‿260‿1.6 Peak (White×(⊢×2⋆¯4⊸×)∘I∘≠)300⥊90) Add +˝ 2‿8‿60 (√∘⊣÷˜1(-÷+)·⋆∘-×⌜) 20 Hp2 (((Sine(+10×White)⊸+)×2⋆¯20×I∘≠)lb⥊80) Add ((2⋆¯14⊸×)∘I × ·Triangle⥊⟜37) lb÷2
s ← ∾⥊ {0¨⊸∾ (lb÷2) ↑ Clip⌾(×⟜3) 8‿80‿1 Peak ¯7‿7e3‿2 HShelf 9‿300‿1.2 Peak √⌾| ((•math.Sin +`∘((2×π)|𝕩÷˜↕))×1⌊1.4×2⋆¯11×I)7e3}¨ 7+100÷˜1-˜1‿2‿3‿1×⌜5‿3‿2‿1
h ← (lb÷4) ↑ 0.8 × (+´¨⊔⟜÷1‿7‿14‿40) Reverb 2e3 Hp1 (¬⊸(×+0.2×⊣)×White) I 1200

(2×lm) Write ⟨k,e,r,y,d,b,l,s,h⟩

er ← e+(lm÷2)⥊r
M ← {𝕩×(≢𝕩)⥊lb/𝕨}
_fg ← {lm⊸(𝔽∘I∘⊣×⥊)⎉1}
F ← (¬×˜)_fg
_cn ← {(××0⌈(𝔽 I lm)-˜|)lm⥊⊢}
WriteFull ∾≍ Stereo¨ (÷⟜2⌾(¯1⊸⊑)lm¨)⊸((+⎉1´⥊⎉1¨)¨) ⟨
  ⟨×˜∘⌽_cn e⟩
  ⟨er,0.4‿¯0.2‿2.6‿¯1.8⊸{+⟜(𝕩⊸×)´𝕨}_fg(lm÷4)↑b⟩
  ⟨er                ,b⟩
  ⟨er,(4×lb)↑(lb÷2)↑l,b , ⥊(1‿2‿1‿3>⌜2⌽↕4)×⌜k1⟩
  ⟨er,        l,k,⥊(2/⥊3‿1/∨⌜˜↕2)×⌜h,s,b⟩
  ⟨¬⊸×_fg y,r,l,k,h,s,b,(0.3××˜)_cn e⟩
  ⟨d,       r,l,k,h,s,b,0.3 _cn e⟩
  ⟨d,     y,r,l,k,h,s,b⟩
  ⟨d,     y,r,l,    s,b×0.6,(0.3××˜∘⌽)_cn e⟩
  ⟨d,     y,r,l,    s,b×0.6,e⟩
  ⟨       y,r,l,k,h,s,b,    e⟩
  ⟨d,     y,r,l,k,h,s,b,    e⟩
  ⟨d,c,   y,r,l,k,h,s,b,    e⟩
  ⟨d,     y,r,l,k,h,s,b,    e⟩
  ⟨d,c,   y,r,l,k,h,s,1.2‿0.8‿0.6 M b,e⟩
  ⟨d,   F y,r,l,  h,s,1.4‿0‿0     M b,e⟩
  ⟨d,       r,l,F h,s,1.4‿0‿0     M b⟩
  ⟨r,s⟩
⟩
